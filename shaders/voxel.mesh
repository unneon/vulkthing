#version 460

#extension GL_EXT_mesh_shader : require
#extension GL_EXT_shader_8bit_storage : require
#extension GL_EXT_shader_16bit_storage : require
#extension GL_EXT_shader_explicit_arithmetic_types_int8 : require
#extension GL_EXT_shader_explicit_arithmetic_types_int16 : require

#include "types/uniform.glsl"

layout(binding = 0) uniform GLOBAL_UNIFORM_TYPE global;
layout(binding = 2) readonly buffer Vertices {
    VoxelVertex vertices[];
};
layout(binding = 3) readonly buffer Triangles {
    VoxelTriangle triangles[];
};
layout(binding = 4) readonly buffer Meshlets {
    VoxelMeshlet meshlets[];
};

// TODO: Mesa advertises 128 maxPreferredMeshWorkGroupInvocations contradicting their presentation and official drivers.
layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;
layout(triangles, max_vertices = 128, max_primitives = 256) out;

layout(location = 0) out vec3 frag_position[];
layout(location = 1) perprimitiveEXT out uint frag_data[];
layout(location = 2) perprimitiveEXT out vec3 frag_color[];

#include "util/oklab.glsl"

vec3 color_from_index(uint index) {
    float lightness = 0.6 + 0.4 * (index * 2971 % 256) / 255.0;
    float chroma = 0.6 + 0.4 * (index * 1933 % 256) / 255.0;
    float hue = 2 * acos(-1) * (index * 907 % 256) / 255.0;
    return srgb_from_oklab(oklab_from_oklch(vec3(lightness, chroma, hue)));
}

void main() {
    VoxelMeshlet meshlet = meshlets[gl_WorkGroupID.x];
    SetMeshOutputsEXT(meshlet.vertex_count, meshlet.triangle_count);

    if (gl_LocalInvocationID.x < meshlet.triangle_count) {
        VoxelTriangle triangle = triangles[meshlet.triangle_offset + gl_LocalInvocationID.x];
        gl_PrimitiveTriangleIndicesEXT[gl_LocalInvocationID.x] = triangle.indices;
        frag_data[gl_LocalInvocationID.x] = triangle.data;
        vec3 work_group_color = color_from_index(gl_WorkGroupID.x);
        vec3 local_invocation_color = color_from_index(gl_LocalInvocationID.x);
        vec3 color = 0.97 * work_group_color + 0.03 * local_invocation_color;
        frag_color[gl_LocalInvocationID.x] = color;
    }

    if (gl_LocalInvocationID.x < meshlet.vertex_count) {
        VoxelVertex vertex = vertices[meshlet.vertex_offset + gl_LocalInvocationID.x];
        vec3 world_space = vertex.position.xyz + 64 * vec3(meshlet.chunk);
        vec4 clip_space = global.camera.projection_matrix * global.camera.view_matrix * vec4(world_space, 1);
        gl_MeshVerticesEXT[gl_LocalInvocationID.x].gl_Position = clip_space;
        frag_position[gl_LocalInvocationID.x] = world_space.xyz;
    }
}
